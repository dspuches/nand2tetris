// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // determine if any control bits are set (load into register)
    Or (a=inc, b=load, out=incOrLoad);
    Or (a=reset, b=incOrLoad, out=incOrLoadOrReset);

    // process inc
    Inc16 (in=loop, out=outPlusOne);
    Mux16 (a=in, b=outPlusOne, sel=inc, out=incOut);

    // process load
    Mux16 (a=incOut, b=in, sel=load, out=loadOut);

    // process reset
    And16 (a=false, b=false, out=zeroOut);
    Mux16 (a=loadOut, b=zeroOut, sel=reset, out=resetOut);

    // pass to register
    Register (in=resetOut, load=incOrLoadOrReset, out=out, out=loop);
}
